plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.4-SNAPSHOT" apply false
    id "com.github.breadmoirai.github-release" version "2.4.1"
}

if (System.getenv("GITHUB_TOKEN") != null) {
    githubRelease {
        token(System.getenv("GITHUB_TOKEN"))
        owner("JamCoreModding")
        repo("jamlib")
        tagName(project.version)
        releaseName("V${project.version} [${minecraft_version}]")
        body(project.rootProject.file("CHANGELOG.md").text)
        draft(false)

        if (project.branch != null) {
            targetCommitish(project.branch)
        }
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    jar {
        manifest {
            attributes([
                    'Specification-Title'   : mod_name,
                    'Specification-Version' : project.jar.archiveVersion,
                    'Implementation-Title'  : project.name,
                    'Implementation-Version': project.jar.archiveVersion,
                    'Built-On-Java'         : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                    'Built-On-Minecraft'    : minecraft_version
            ])
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.officialMojangMappings()
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    version = rootProject.version
    group = rootProject.group

    repositories {
        mavenCentral()

        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    tasks.create("prepareWorkspace") {}

    java {
        withSourcesJar()
    }

    processResources {
        filesMatching(["pack.mcmeta", "fabric.mod.json", "mods.toml", "*.mixins.json", "quilt.mod.json"]) {
            expand project.properties
        }
    }

    tasks.publish {
        dependsOn("githubRelease")
        dependsOn("fabric:publishUnified")
        dependsOn("quilt:publishUnified")
        dependsOn("forge:publishUnified")
    }
}
